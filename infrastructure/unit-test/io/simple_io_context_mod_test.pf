!-----------------------------------------------------------------------------
! (C) Crown copyright 2020 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> @brief Exercises the simple I/O context.
!>
module simple_io_context_mod_test

  use clock_mod,             only : clock_type
  use constants_mod,         only : r_second
  use io_context_mod,        only : io_context_type, &
                                    io_context_initialiser_type
  use pfunit_mod
  use simple_io_context_mod, only : simple_io_context_type

  implicit none

  private
  public :: test_initialisation

  type, extends(io_context_initialiser_type) :: telltale_callback_type
    private
    logical, public :: flagged
  contains
    private
    procedure, public :: callback
  end type telltale_callback_type

contains

  subroutine callback( this, context )
    implicit none
    class(telltale_callback_type), intent(inout) :: this
    class(io_context_type),        intent(in) :: context
    ! Check clock has been set up at this point.
    call clock_val( context%get_clock() )
    this%flagged = .true.
  end subroutine callback

  @test
  subroutine test_initialisation()

    implicit none

    type(simple_io_context_type) :: test_unit
    type(telltale_callback_type) :: telltale_callback
    class(clock_type), pointer   :: clock

    telltale_callback%flagged = .false.
    call test_unit%initialise( telltale_callback, &
                               '4', '7',          &
                               2.5_r_second, 1.2_r_second )
    @assertTrue( telltale_callback%flagged )
    ! Check clock is still correct.

    call clock_val( test_unit%get_clock() )

  end subroutine test_initialisation

  subroutine clock_val( clock )

    implicit none

    class(clock_type), intent(in), pointer :: clock

    @assertEqual( 4, clock%get_step() )
    @assertEqual( 4, clock%get_first_step() )
    @assertEqual( 7, clock%get_last_step() )
    @assertFalse( clock%is_spinning_up() )
    @assertEqual( 1.2_r_second, clock%get_seconds_per_step() )

  end subroutine clock_val

end module simple_io_context_mod_test
