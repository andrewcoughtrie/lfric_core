!-----------------------------------------------------------------------------
! (C) Crown copyright 2019 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> Test the halo_routing representation
!>
!-------------------------------------------------------------------------------
module halo_routing_mod_test

  use constants_mod,                  only : i_def, r_def, real_type
  use halo_routing_mod,               only : halo_routing_type
  use fs_continuity_mod,              only : W3
  use function_space_collection_mod,  only : function_space_collection_type, &
                                             function_space_collection
  use local_mesh_mod,                 only : local_mesh_type
  use mesh_collection_mod,            only : mesh_collection_type, &
                                             mesh_collection
  use mesh_mod,                       only : mesh_type, PLANE_BI_PERIODIC
  use yaxt,                           only : xt_initialize, xt_finalize
  use mpi_mod,                        only : store_comm, clear_comm
  use pFUnit_Mod

  implicit none

  private
  public :: test_halo_routing

  @TestCase
  type, extends(MPITestCase), public :: halo_routing_test_type
    private
    type(local_mesh_type) :: unit_test_local_mesh
    integer(i_def) :: mesh_id
  contains
    procedure setUp
    procedure tearDown
    procedure get_local_mesh_ptr
    procedure test_halo_routing
  end type halo_routing_test_type

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    implicit none

    class(halo_routing_test_type), intent(inout) :: this

    type(local_mesh_type), pointer :: unit_test_local_mesh_ptr
    type(mesh_type) :: unit_test_mesh

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    !Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    call this%unit_test_local_mesh%initialise()
    unit_test_local_mesh_ptr => this%get_local_mesh_ptr()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    unit_test_mesh = mesh_type( PLANE_BI_PERIODIC, unit_test_local_mesh_ptr )
    this%mesh_id = mesh_collection%add_new_mesh( unit_test_mesh )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    implicit none

    class(halo_routing_test_type), intent(inout) :: this

    call function_space_collection%clear()
    call mesh_collection%clear()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  function get_local_mesh_ptr(this) result(unit_test_local_mesh_ptr)
    implicit none
    class(halo_routing_test_type), intent(inout), target :: this
    type(local_mesh_type), pointer :: unit_test_local_mesh_ptr
    unit_test_local_mesh_ptr => this%unit_test_local_mesh
  end function get_local_mesh_ptr

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  @Test( npes=[1] )
  subroutine test_halo_routing( this )

    implicit none

    class(halo_routing_test_type), intent(inout) :: this

    type(halo_routing_type) :: halo_routing

    integer(i_def) :: result

    type(mesh_type), pointer :: mesh => null()

    mesh => mesh_collection%get_mesh(this%mesh_id)

    ! Create a halo_routing object with a particular mesh_id, element_order,
    ! lfric_fs, ndata, type and kind
    halo_routing = halo_routing_type( mesh, &
                                      0, &
                                      W3, &
                                      2, &
                                      real_type, &
                                      r_def )

    ! Test the getters on the halo_routing object return the correct values
    result = halo_routing%get_mesh_id()
    @assertEqual( this%mesh_id, result )

    result = halo_routing%get_element_order()
    @assertEqual( 0, result )

    result = halo_routing%get_lfric_fs()
    @assertEqual( W3, result )

    result = halo_routing%get_ndata()
    @assertEqual( 2, result )

    result = halo_routing%get_fortran_type()
    @assertEqual( real_type, result )

    result = halo_routing%get_fortran_kind()
    @assertEqual( r_def, result )

    mesh => null()

  end subroutine test_halo_routing

end module halo_routing_mod_test
