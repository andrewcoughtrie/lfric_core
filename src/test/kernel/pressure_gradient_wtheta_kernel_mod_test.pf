!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

!> Test the pressure gradient computation
!>
module pressure_gradient_wtheta_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type, &
                                            field_proxy_type
  use function_space_mod,            only : function_space_type
  use fs_continuity_mod,             only : Wtheta, W2, W3
  use pFUnit_Mod
  use quadrature_mod,                only : quadrature_type, GAUSSIAN

  implicit none

  private
  public :: pressure_gradient_wtheta_test_type, test_all

  @TestCase
  type, extends(TestCase) :: pressure_gradient_wtheta_test_type
    private
    type(quadrature_type)              :: qr
    type(function_space_type), pointer :: wtheta_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(field_type)                   :: r_u, rho, theta
    real(r_def), allocatable           :: basis_w3(:,:,:,:), &
                                          basis_w2(:,:,:,:), &
                                          basis_wtheta(:,:,:,:), &
                                          diff_basis_wtheta(:,:,:,:), &
                                          diff_basis_w2(:,:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type pressure_gradient_wtheta_test_type

  integer(i_def), parameter :: element_order = 0

  real(r_def), parameter :: bvf_square = 0.0001_r_def

  real(r_def), parameter :: gravity = 10.0_r_def
  real(r_def), parameter :: radius  = 6000000_r_def
  real(r_def), parameter :: omega   = 8.0E-5_r_def
  real(r_def), parameter :: p_zero  = 100000.0_r_def
  real(r_def), parameter :: rd      = 300.0_r_def
  real(r_def), parameter :: cp      = 1000.0_r_def
  real(r_def), parameter :: scaling = 1.0_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use base_mesh_config_mod,      only : base_mesh_geometry_planar, &
                                          base_mesh_partitioner_planar
    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use feign_config_mod,           only : feign_base_mesh_config,           &
                                          feign_initial_temperature_config, &
                                          feign_planet_config
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class(pressure_gradient_wtheta_test_type), intent(inout) :: this

    type(field_proxy_type)    :: r_u_proxy, rho_proxy, theta_proxy

    integer(i_def) :: nqp_h, nqp_v
    integer(i_def) :: ndf_wtheta, ndf_w2, ndf_w3
    integer(i_def) :: undf_wtheta, undf_w2, undf_w3
    integer(i_def) :: dim_wtheta, diff_dim_wtheta, dim_w2, diff_dim_w2, dim_w3

    integer(i_def) :: mesh_id

    call feign_base_mesh_config( 'foo',                        &
                                 base_mesh_geometry_planar,    &
                                 base_mesh_partitioner_planar, &
                                 .false., 0.0_r_def)
    call feign_initial_temperature_config( bvf_square )
    call feign_planet_config( cp, gravity, omega, p_zero, radius, rd, scaling )

    reference_element = finite_element_shape_quadrilateral
    call reference_cube()

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    this%qr = quadrature_type( 3, GAUSSIAN )

    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%wtheta_fs => function_space_collection%get_fs(mesh_id, element_order, Wtheta)
    this%w2_fs => function_space_collection%get_fs(mesh_id, element_order, W2)
    this%w3_fs => function_space_collection%get_fs(mesh_id, element_order, W3)

    this%rho   = field_type( vector_space = this%w3_fs )
    rho_proxy = this%rho%get_proxy()

    ndf_w3  = rho_proxy%vspace%get_ndf( )
    dim_w3  = rho_proxy%vspace%get_dim_space( )
    undf_w3 = rho_proxy%vspace%get_undf()
    allocate(this%basis_w3(dim_w3,ndf_w3,nqp_h,nqp_v))

    this%r_u   = field_type( vector_space = this%w2_fs )
    r_u_proxy = this%r_u%get_proxy()

    ndf_w2      = r_u_proxy%vspace%get_ndf( )
    dim_w2      = r_u_proxy%vspace%get_dim_space( )
    diff_dim_w2 = r_u_proxy%vspace%get_dim_space_diff( )
    undf_w2     = r_u_proxy%vspace%get_undf()
    allocate(this%basis_w2(dim_w2,ndf_w2,nqp_h,nqp_v))
    allocate(this%diff_basis_w2(diff_dim_w2,ndf_w2,nqp_h,nqp_v))

    this%theta = field_type( vector_space = this%wtheta_fs )
    theta_proxy = this%theta%get_proxy()

    ndf_wtheta      = theta_proxy%vspace%get_ndf( )
    dim_wtheta      = theta_proxy%vspace%get_dim_space( )
    diff_dim_wtheta = theta_proxy%vspace%get_dim_space_diff( )
    undf_wtheta     = theta_proxy%vspace%get_undf()
    allocate(this%basis_wtheta(dim_wtheta,ndf_wtheta,nqp_h,nqp_v))
    allocate(this%diff_basis_wtheta(diff_dim_wtheta,ndf_wtheta,nqp_h,nqp_v))

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(pressure_gradient_wtheta_test_type), intent(inout) :: this

    deallocate( this%basis_w3,                     &
                this%basis_w2, this%diff_basis_w2, &
                this%basis_wtheta, this%diff_basis_wtheta )

    call deallocate_reference()
    call function_space_collection%clear()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use pressure_gradient_wtheta_kernel_mod, only : pressure_gradient_wtheta_code

    implicit none

    class(pressure_gradient_wtheta_test_type), intent(inout) :: this

    real(r_def), parameter :: tol_v      = 1.0e-3_r_def
    real(r_def), parameter :: tol_h      = 1.0e+1_r_def
    real(r_def), parameter :: answer_v   = 0.0_r_def
    real(r_def), parameter :: answer_h   = -0.2202941E+10_r_def

    type(field_type) :: chi(3)
    type(field_proxy_type )   :: chi_p(3)

    integer :: cell, k

    integer                 :: nlayers, nqp_h, nqp_v
    integer                 :: ndf_wtheta, ndf_w2, ndf_w3
    integer                 :: undf_wtheta, undf_w2, undf_w3
    integer                 :: element_order
    integer, pointer        :: map_w3(:), map_w2(:), map_wtheta(:) => null()

    type(field_proxy_type) :: r_u_proxy, rho_proxy, theta_proxy

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:) => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()

    ! Test kernel
    r_u_proxy  = this%r_u%get_proxy()
    rho_proxy  = this%rho%get_proxy()
    theta_proxy = this%theta%get_proxy()

    nlayers = rho_proxy%vspace%get_nlayers()
    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()
    zp=>this%qr%get_xqp_v()
    xp=>this%qr%get_xqp_h()
    wh=>this%qr%get_wqp_h()
    wv=>this%qr%get_wqp_v()


    ndf_w3  = rho_proxy%vspace%get_ndf( )
    undf_w3 = rho_proxy%vspace%get_undf()

    ndf_w2  = r_u_proxy%vspace%get_ndf( )
    undf_w2 = r_u_proxy%vspace%get_undf()

    ndf_wtheta  = theta_proxy%vspace%get_ndf( )
    undf_wtheta = theta_proxy%vspace%get_undf()

    call rho_proxy%vspace%compute_basis_function( &
         this%basis_w3, ndf_w3, nqp_h, nqp_v, xp, zp )

    call r_u_proxy%vspace%compute_basis_function( &
         this%basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp )

    call r_u_proxy%vspace%compute_diff_basis_function( &
         this%diff_basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp )

    call theta_proxy%vspace%compute_basis_function( &
         this%basis_wtheta, ndf_wtheta, nqp_h, nqp_v, xp, zp )

    call theta_proxy%vspace%compute_diff_basis_function( &
         this%diff_basis_wtheta, ndf_wtheta, nqp_h, nqp_v, xp, zp )

    cell = 1

    map_w3 => rho_proxy%vspace%get_cell_dofmap( cell )
    map_w2 => r_u_proxy%vspace%get_cell_dofmap( cell )
    map_wtheta => theta_proxy%vspace%get_cell_dofmap( cell )

    ! Create the data
    r_u_proxy%data(:) = 0.0_r_def
    rho_proxy%data(:) = 1.5_r_def
    theta_proxy%data(:) = 300.0_r_def

    call pressure_gradient_wtheta_code( nlayers,                                   &
                                        r_u_proxy%data,                            &
                                        rho_proxy%data,                            &
                                        theta_proxy%data,                          &
                                        ndf_w2, undf_w2,                           &
                                        map_w2, this%basis_w2, this%diff_basis_w2, &
                                        ndf_w3, undf_w3,                           &
                                        map_w3, this%basis_w3,                     &
                                        ndf_wtheta, undf_wtheta,                   &
                                        map_wtheta, this%basis_wtheta,             &
                                        this%diff_basis_wtheta,                    &
                                        nqp_h, nqp_v, wh, wv                       &
                                        )

    k = 1
    ! Test there are no contibutions on the horizontal faces
    @assertEqual(answer_v, r_u_proxy%data(map_w2(5)+k), tol_v)
    @assertEqual(answer_v, r_u_proxy%data(map_w2(6)+k), tol_v)
    ! Test contibution on a vertical face
    @assertEqual(answer_h, r_u_proxy%data(map_w2(4)+k), tol_h)

  end subroutine test_all

end module pressure_gradient_wtheta_kernel_mod_test
