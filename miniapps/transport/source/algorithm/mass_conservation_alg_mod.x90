!-----------------------------------------------------------------------------
! (c) Crown copyright 2018 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> @brief Algorithm to compute the total mass.
!> @details Total mass is calculated by multiplying the density value in each
!>         cell by the volume of the cell and summing all cells.
module mass_conservation_alg_mod

  use compute_moist_mass_kernel_mod,   only: compute_moist_mass_kernel_type
  use compute_total_mass_kernel_mod,   only: compute_total_mass_kernel_type
  use constants_mod,                   only: r_def, i_def
  use fem_constants_mod,               only: get_qr
  use function_space_collection_mod,   only: function_space_collection
  use field_mod,                       only: field_type
  use finite_element_config_mod,       only: element_order
  use fs_continuity_mod,               only: W3
  use function_space_mod,              only: function_space_type
  use log_mod,                         only: log_event,         &
                                             log_scratch_space, &
                                             LOG_LEVEL_INFO
  use mesh_mod,                        only: mesh_type
  use mr_indices_mod,                  only: nummr
  use quadrature_xyoz_mod,             only: quadrature_xyoz_type
  use quadrature_rule_gaussian_mod,    only: quadrature_rule_gaussian_type
  use geometric_constants_mod,         only: get_coordinates, get_panel_id
  use io_config_mod,                   only: subroutine_timers
  use timer_mod,                       only: timer

  implicit none

  private
  public :: mass_conservation

contains

  !> @brief Subroutine to compute the total mass
  !> @param[in] tstep       Current timestep
  !> @param[in] density     Density field
  !> @param[in] mr          Bundle of mixing ratio fields
  subroutine mass_conservation( tstep, density, mr )

    implicit none

    integer(kind=i_def),     intent(in) :: tstep
    type(field_type),        intent(in) :: density
    type(field_type),        intent(in) :: mr(nummr)

    type(field_type),           pointer :: chi(:) => null()
    type(field_type),           pointer :: panel_id => null()
    type(function_space_type),  pointer :: w3_fs => null()
    type(quadrature_xyoz_type), pointer :: qr => null()

    type(field_type)      :: dry_mass_field, water_mass_field
    real(kind=r_def)      :: total_dry_mass, total_water_mass

    type(mesh_type), pointer   :: mesh => null()

    if ( subroutine_timers ) call timer( 'mass_conservation' )

    mesh => density%get_mesh()

    chi => get_coordinates(mesh%get_id())
    panel_id => get_panel_id(mesh%get_id())

    ! Get a quadrature rule
    qr => get_qr()

    ! Create mass fields
    w3_fs => function_space_collection%get_fs( mesh, element_order, W3 )
    call dry_mass_field%initialise( vector_space = w3_fs )
    call water_mass_field%initialise( vector_space = w3_fs )

    ! Compute masses
    call invoke( compute_total_mass_kernel_type( dry_mass_field, density,      &
                                                 chi, panel_id, qr ),          &
                 sum_X( total_dry_mass, dry_mass_field ),                      &
                 compute_moist_mass_kernel_type( water_mass_field, mr(1),      &
                                                 density, chi, panel_id, qr ), &
                 sum_X( total_water_mass, water_mass_field ) )

    ! Write out masses
    write( log_scratch_space, '(A,I4,1E32.24)')  &
                  'Total dry mass  = ', tstep, total_dry_mass
    call log_event( log_scratch_space, LOG_LEVEL_INFO )
    write( log_scratch_space, '(A,I4,1E32.24)')  &
                  'Total water mass  = ', tstep, total_water_mass
    call log_event( log_scratch_space, LOG_LEVEL_INFO )

    nullify( chi, panel_id, w3_fs, qr, mesh )

    if ( subroutine_timers ) call timer( 'mass_conservation' )

  end subroutine mass_conservation

end module mass_conservation_alg_mod
