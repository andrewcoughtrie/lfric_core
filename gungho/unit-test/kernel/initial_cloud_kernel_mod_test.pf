!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the initial_cloud_kernel
!>

module initial_cloud_kernel_mod_test

  use constants_mod,                 only : r_def, i_def
  use pFUnit_Mod
  use yaxt,                          only : xt_initialize, xt_finalize
  use mpi_mod,                       only : store_comm, clear_comm

  implicit none

  private
  public:: test_of_initial_cloud

  @TestCase
  type, extends(MPITestCase), public  :: cloud_test_type
    private
    real(kind=r_def), allocatable :: answer_cfarea(:), answer_cfice(:),  &
                                     answer_cfliq(:),  answer_cfbulk(:), &
                                     answer_rh_crit_wth(:), cfarea(:),   &
                                     cfliq(:), cfice(:), cfbulk(:),      &
                                     rh_crit_wth(:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_of_initial_cloud
  end type cloud_test_type

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use gungho_feign_config_mod, only : feign_physics_config,           &
                                        feign_cloud_um_config,          &
				        feign_extrusion_config
    use physics_config_mod, only : physics_cloud_scheme_smith,          &
                                   physics_radiation_placement_none,    &
				   physics_blayer_placement_none,       &
                                   physics_convection_placement_none,   &
				   physics_microphysics_placement_none
    use extrusion_config_mod, only: extrusion_method_uniform

    implicit none

    class(cloud_test_type), intent(inout) :: this

    integer          :: undf_wth
    integer          :: nlayers

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    ! Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    undf_wth  = 1_i_def
    nlayers  = 3_i_def

    call feign_physics_config( blayer_placement                           &
                                = physics_blayer_placement_none,          &
                               convection_placement                       &
                                = physics_convection_placement_none,      &
                               radiation_placement                        &
                                = physics_radiation_placement_none,       &
                               heldsuarez_thermo_placement=-1,            &
                               heldsuarez_wind_placement=-1,              &
                               hs_random=.false.,                         &
                               cloud_scheme = physics_cloud_scheme_smith, &
                               microphysics_placement                     &
                                = physics_microphysics_placement_none)

   call feign_extrusion_config( method=extrusion_method_uniform, &
                             	domain_top=1000.0_r_def,         &
                             	number_of_layers=nlayers )


    call feign_cloud_um_config(rh_crit=[0.96_r_def,0.9_r_def,0.8_r_def])

    allocate( this%answer_cfarea(undf_wth + nlayers) )
    allocate( this%answer_cfliq(undf_wth + nlayers) )
    allocate( this%answer_cfice(undf_wth + nlayers) )
    allocate( this%answer_cfbulk(undf_wth + nlayers) )
    allocate( this%answer_rh_crit_wth(undf_wth + nlayers) )

    allocate( this%cfarea(undf_wth + nlayers) )
    allocate( this%cfliq(undf_wth + nlayers) )
    allocate( this%cfice(undf_wth + nlayers) )
    allocate( this%cfbulk(undf_wth + nlayers) )
    allocate( this%rh_crit_wth(undf_wth + nlayers) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use gungho_configuration_mod, only: final_configuration

    implicit none

    class(cloud_test_type), intent(inout) :: this

    deallocate( this%answer_cfarea, this%answer_cfliq,  &
                this%answer_cfice,  this%answer_cfbulk, &
                this%answer_rh_crit_wth, this%cfarea,   &
                this%cfliq, this%cfice, this%cfbulk,    &
                this%rh_crit_wth)

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

    ! Finalise namelists
    call final_configuration()


  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_of_initial_cloud( this )

    use initial_cloud_kernel_mod, only : initial_cloud_code

    implicit none

    class(cloud_test_type), intent(inout) :: this

    real(kind=r_def), parameter   :: tol = 1.0e-14_r_def

    integer, allocatable          :: map_wth(:)
    integer          :: nlayers
    integer          :: ndf_wth
    integer          :: undf_wth

    nlayers  = 3_i_def
    ndf_wth  = 1_i_def
    undf_wth = 1_i_def

    allocate(map_wth(ndf_wth))
    map_wth(:)  = 1_i_def

    ! Set correct answers
    this%answer_cfarea(:) = 0.0_r_def
    this%answer_cfliq(:)  = 0.0_r_def
    this%answer_cfice(:)  = 0.0_r_def
    this%answer_cfbulk(:) = 0.0_r_def
    this%answer_rh_crit_wth(:) = (/ 0.96_r_def, 0.96_r_def, 0.9_r_def, 0.8_r_def /)

    ! Make up initial wrong fields    
    this%cfarea(:) = -9.0_r_def
    this%cfliq(:)  = -9.0_r_def
    this%cfice(:)  = -9.0_r_def
    this%cfbulk(:) = -9.0_r_def
    this%rh_crit_wth(:) = -9.0_r_def

    call initial_cloud_code(nlayers,            &
                           this%cfarea(:),      &
                           this%cfice(:),       &
                           this%cfliq(:),       &
                           this%cfbulk(:),      &
                           this%rh_crit_wth(:), &
                           ndf_wth,             & 
                           undf_wth,            &
                           map_wth)

    @assertEqual( this%answer_cfarea(:), this%cfarea(:), tol )
    @assertEqual( this%answer_cfice(:),  this%cfice(:), tol )
    @assertEqual( this%answer_cfliq(:),  this%cfliq(:), tol )
    @assertEqual( this%answer_cfbulk(:), this%cfbulk(:), tol )
    @assertEqual( this%answer_rh_crit_wth(:), this%rh_crit_wth(:), tol )

  end subroutine test_of_initial_cloud

end module initial_cloud_kernel_mod_test
