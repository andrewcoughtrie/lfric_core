!-----------------------------------------------------------------------------
! (C) Crown copyright 2023 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> This code tests the fractional vertical wind kernel

module fractional_vertical_wind_kernel_mod_test

  use constants_mod, only : i_def, r_tran
  use pFUnit_Mod

  implicit none

  private
  public :: test_all

  @TestCase
  type, public, extends(TestCase) :: fractional_vertical_wind_test_type
    private
  contains
    procedure test_all
  end type fractional_vertical_wind_test_type

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use, intrinsic :: iso_fortran_env,       only : real64
    use fractional_vertical_wind_kernel_mod, only : fractional_vertical_wind_code

    implicit none

    class(fractional_vertical_wind_test_type), intent(inout) :: this

    real(r_tran),   parameter :: tol       = 1.0e-12_r_tran   ! r_tran 64bit
    integer(i_def), parameter :: nlayers   = 5
    integer(i_def), parameter :: ndf_w2v   = 2
    integer(i_def), parameter :: undf_w2v  = nlayers+1
    integer(i_def), dimension(ndf_w2v) :: map_w2v

    real(r_tran), dimension(undf_w2v) :: frac_wind

    real(r_tran) :: dep_pts(6)
    real(r_tran) :: detj(6)
    real(r_tran) :: dt
    real(r_tran) :: answer(1:6)

    real(r_tran)   :: use_tol
    integer(i_def) :: k

    ! Set up maps
    map_w2v(:) = (/ 1, 2 /)

    ! Set Det(J) and time step
    detj = (/ 1.0_r_tran, 2.0_r_tran, 4.0_r_tran, 6.0_r_tran, 8.0_r_tran, 10.0_r_tran /)
    dt   = 2.0_r_tran

    ! Set departure points
    dep_pts = (/ 0.0_r_tran, 0.5_r_tran, 1.2_r_tran, -1.3_r_tran, -0.8_r_tran, 0.0_r_tran /)

    ! Answer is fractional_departure_distance * Det(j) / dt
    answer = (/ 0.0_r_tran,  0.5_r_tran,  0.4_r_tran,  -0.9_r_tran,  -3.2_r_tran,  0.0_r_tran /)

    call fractional_vertical_wind_code( nlayers,   &
                                        frac_wind, &
                                        dep_pts,   &
                                        detj,      &
                                        dt,        &
                                        ndf_w2v,   &
                                        undf_w2v,  &
                                        map_w2v  )

    if ( r_tran == real64 ) then
      use_tol = tol
    else
      ! Set tolerance based on precision of field
      use_tol = 10.0_r_tran*spacing( maxval(dep_pts(:)) )
    endif

    do k=1,nlayers+1
      @assertEqual( answer(k), frac_wind(k), use_tol)
    end do

  end subroutine test_all

end module fractional_vertical_wind_kernel_mod_test
