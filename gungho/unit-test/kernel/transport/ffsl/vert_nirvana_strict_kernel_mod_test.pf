!-----------------------------------------------------------------------------
! (C) Crown copyright 2023 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> This code tests Nirvana with strict limiter in the vertical.

module vert_nirvana_strict_kernel_mod_test

  use constants_mod, only : i_def, r_tran
  use pFUnit_Mod

  implicit none

  private
  public :: test_all

  @TestCase
  type, public, extends(TestCase) :: vert_nirvana_strict_test_type
    private
  contains
    procedure test_all
  end type vert_nirvana_strict_test_type

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use, intrinsic :: iso_fortran_env,  only : real64
    use vert_nirvana_strict_kernel_mod, only : vert_nirvana_strict_code

    implicit none

    class(vert_nirvana_strict_test_type), intent(inout) :: this

    real(r_tran),   parameter :: tol       = 1.0e-12_r_tran   ! r_tran 64bit
    integer(i_def), parameter :: nlayers   = 5
    integer(i_def), parameter :: ndf_w3    = 1
    integer(i_def), parameter :: ndf_w2v   = 2
    integer(i_def), parameter :: undf_w3   = nlayers
    integer(i_def), parameter :: undf_w2v  = nlayers+1
    integer(i_def), dimension(ndf_w3)  :: map_w3
    integer(i_def), dimension(ndf_w2v) :: map_w2v

    real(r_tran), dimension(undf_w2v) :: flux_high
    real(r_tran), dimension(undf_w2v) :: frac_wind

    real(r_tran) :: field(5)
    real(r_tran) :: dz(5)
    real(r_tran) :: dep_pts(6)
    real(r_tran) :: answer_high(1:6)

    real(r_tran)   :: use_tol
    integer(i_def) :: k

    ! Set up maps
    map_w3(1)= 1
    map_w2v(:) = (/ 1, 2 /)

    ! Set constant grid spacing for Nirvana test
    dz = (/ 1.0_r_tran, 1.0_r_tran, 1.0_r_tran, 1.0_r_tran, 1.0_r_tran /)

    ! Use field to generate extrema
    field = (/ 1.0_r_tran, 4.0_r_tran, 9.0_r_tran, 0.0_r_tran, 1.0_r_tran /)
    dep_pts = (/ 0.0_r_tran, 0.2_r_tran, 0.5_r_tran, -1.2_r_tran, -0.2_r_tran, 0.0_r_tran /)
    frac_wind = (/ 0.0_r_tran, 1.0_r_tran, 2.0_r_tran, -2.0_r_tran, -1.0_r_tran, 0.0_r_tran /)
    answer_high = (/ 0.0_r_tran,  1.0_r_tran,  10.0_r_tran, -2.0_r_tran,  -1.0_r_tran,  0.0_r_tran /)

    call vert_nirvana_strict_code( nlayers,   &
                                   flux_high, &
                                   frac_wind, &
                                   dep_pts,   &
                                   field,     &
                                   dz,        &
                                   ndf_w2v,   &
                                   undf_w2v,  &
                                   map_w2v,   &
                                   ndf_w3,    &
                                   undf_w3,   &
                                   map_w3 )

    if ( r_tran == real64 ) then
      use_tol = tol
    else
      ! Set tolerance based on precision of field
      use_tol = 10.0_r_tran*spacing( maxval(field(:)) )
    endif

    do k=1,nlayers+1
      @assertEqual( answer_high(k), flux_high(k), use_tol)
    end do

  end subroutine test_all

end module vert_nirvana_strict_kernel_mod_test
